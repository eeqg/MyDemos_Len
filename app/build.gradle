/**
 * app/build.gradle 默认三个小节点：apply、android和dependencies。
 *
 * apply是一个方法，给它传递了一个参数plugin，plugin 的值是'com.android.application'。
 * 如果有多个参数，则以逗号隔开，例如：compile name: 'volley', ext: 'aar'。
 */

apply plugin: 'com.android.application'

/**
 * android 项目配置
 */
android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "com.example.mydemos_len"
        minSdkVersion 21
        targetSdkVersion 21
        versionName '1.0'
        versionCode 1

        //自定义config熟悉
        buildConfigField "boolean", "AUTO_UPDATES", "true"
        buildConfigField "int", "fortest", "0"
    }

    /**
     * 签名设置
     */
    signingConfigs {
        myConfigs {
            storeFile file("../keystore.jks")
            storePassword "123456"
            keyAlias "wp"
            keyPassword "123456"
        }
    }
    /**
     * buildTypes
     */
    buildTypes {
        //默认是release
        release {
            // 自定义config属性: "LOG_DEBUG" 不显示Log
            buildConfigField "boolean", "LOG_DEBUG", "false"

            minifyEnabled true
            debuggable false

            // 是否进行混淆
            minifyEnabled false
            // 混淆文件的位置
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            //签名
            signingConfig signingConfigs.myConfigs
        }
        debug {
            // 显示Log
            buildConfigField "boolean", "LOG_DEBUG", "true"

            minifyEnabled false
            debuggable true

            // 是否进行混淆
            minifyEnabled false
            // 混淆文件的位置
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            //签名
            signingConfig signingConfigs.myConfigs
        }
    }

    /**
     * 产品渠道
     * Terminal 执行 assembleRelease 或 assembleDebug 可一次性打包.
     */
    productFlavors {
        defaultApk {}
        GooglePlay {
            applicationId "com.example.mydemos_len.google_play"

            buildConfigField "int", "fortest", "1"
        }
        xiaomi {
            applicationId "com.example.mydemos_len.xiaomi"
            versionName '5.1.0'
            versionCode 11

            buildConfigField "boolean", "AUTO_UPDATES", "false"
            buildConfigField "int", "fortest", "2"
        }
        umeng {
            applicationId "com.example.mydemos_len.umeng"
            versionName '2.1.0'
            versionCode 2

            buildConfigField "int", "fortest", "3"
        }
    }

    //修改生成的apk名字
    applicationVariants.all { variant ->

        if (variant.buildType.name.equals('release')) {
            variant.outputs.each { output ->
                def appName = 'MyDemos'
                def oldFile = output.outputFile
                def buildName = 'GooglePlay'
                def releaseApkName

                variant.productFlavors.each { product ->
                    buildName = product.name
                }

                releaseApkName = appName + /*getVersionNameFromManifest()*/versionName + '-' + buildName + '-' + getDate() + '.apk'
                output.outputFile = new File(oldFile.parent, releaseApkName)
            }
        }
    }
}

//获取时间戳
def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMddHHmm')
    return formattedDate
}

//从androidManifest.xml中获取版本号
//def getVersionNameFromManifest() {
//    def manifestParser = new com.android.builder.core.DefaultManifestParser()
//    return manifestParser.getVersionName(android.sourceSets.main.manifest.srcFile)
//}

//声明
repositories {
    flatDir { dirs 'libs' }
}

/**
 * add 依赖库
 *
 * //单文件依赖
 *   compile files('libs/android-support-v4.jar')
 * //某个文件夹下面全部依赖
 *   compile fileTree(dir: 'libs', include: '*.jar')
 */


dependencies {
    compile project(':okhttputils')
    //add gson
    //添加imageselector-release.aar包
    compile(name: "imageselector-release", ext: "aar")
    //add glide
    compile 'com.android.support:support-v4:20.0.0'
    compile 'com.android.support:recyclerview-v7:21.0.3'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.android.support:appcompat-v7:25.3.0'
    compile 'com.android.support:design:25.3.0'
    compile 'com.google.code.gson:gson:2.8.0'
    compile 'io.reactivex:rxjava:1.2.7'
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'com.trello:rxlifecycle:0.3.0'
    compile 'com.trello:rxlifecycle-components:0.3.0'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    //banner view
    compile 'com.bigkoo:convenientbanner:2.0.5'
    //viewpager Transformer
    compile 'com.ToxicBakery.viewpager.transforms:view-pager-transforms:1.2.32@aar'
}
